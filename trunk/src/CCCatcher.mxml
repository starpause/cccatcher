<?xml version="1.0" encoding="utf-8"?>
<mx:WindowedApplication xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" title="CCCatcher" applicationComplete="init();">
	<mx:Style>
	    WindowedApplication { 
	        background-color:"0xFFFFFF";
	        showFlexChrome:"false";
	    } 
	</mx:Style>
    <mx:Script>
        <![CDATA[
        	import view.HitBox;
        	import view.Time;
        	import view.NfoDisplay;
        	import mx.rpc.mxml.Concurrency;
        	import mx.logging.targets.TraceTarget;
            import mx.controls.Alert;
            import mx.core.DragSource;
            import mx.events.DragEvent;
            import flash.events.MouseEvent;
            import mx.controls.Image;
            import mx.managers.DragManager;
            import flash.filesystem.File;
            import flash.events.NativeDragEvent;
			import flash.desktop.NativeDragManager;
			import flash.events.NativeDragEvent;
			import flash.media.Sound;
			import flash.media.SoundChannel;
			import flash.events.NativeWindowBoundsEvent;
			import flash.utils.*;
						
			import flash.events.KeyboardEvent;
			import flash.ui.Keyboard;
			
			import flash.desktop.Clipboard;
			
			import model.Config;
			import view.RandomCover;
			import utils.FileSystem;
			import utils.Utils;
			
			[Embed(source='./assets/SWFIT_SL.TTF', fontName='_swfit')]
			public static var _swfit:Class;
			
			[Embed(source='./assets/_uni05_53.ttf', fontName='_uni05')]
			public static var _uni05:Class;
			
			private var TRANSPORT_MS:int = 7000;
            static private var config:Config = Config.instance;
			private var _songCurrent:Sound = new Sound();
			private var _channel:SoundChannel = new SoundChannel();
			private var cm:ContextMenu = new ContextMenu();
			
			private var songPaused:Boolean = true;
			private var songPosition:Number = 0;
			
			private var randomCover:RandomCover = new RandomCover();
			
			private var timeDisplay:Time = new Time();
			private var nfoDisplay:NfoDisplay = new NfoDisplay();
			private var hitBox:HitBox = new HitBox();
			
			private var shiftKeyDown:Boolean = false;
			
			private var holdingSavedSong:Boolean = false;

			//double click emulation
			private var clickNum:Number = 0
			private var littleTimer:Timer = new Timer(250,1);
			
			private var alphaDiv:Sprite = new Sprite();
			private var alphaValue:Number = .8;

//- SETUP -//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//
            private function init():void {
                stage.nativeWindow.visible = false;
                this.setStyle("backgroundAlpha", .0);
				this.alwaysInFront = true;
				stage.nativeWindow.addEventListener(Event.CLOSING, windowClosingHandler); 
				stage.addEventListener(KeyboardEvent.KEY_DOWN,onKeyDown);
				stage.addEventListener(KeyboardEvent.KEY_UP,onKeyUp);
				
				alphaDiv.alpha = alphaValue;
				alphaDiv.addEventListener(MouseEvent.MOUSE_OVER, onAlphaDivOver);
				alphaDiv.addEventListener(MouseEvent.MOUSE_OUT, onAlphaDivOut);

				stage.addChild(alphaDiv);
				
				//double click emulation
				littleTimer.addEventListener(TimerEvent.TIMER, onBoxClick);
				
				//randomCover.alpha = alphaValue;
				alphaDiv.addChild(randomCover);
				
				//nfo display
				nfoDisplay.visible = false;
				alphaDiv.addChild(nfoDisplay);
				
				//attach sh*t to the hitbox
				hitBox.buttonMode = true;
				hitBox.addEventListener(NativeDragEvent.NATIVE_DRAG_ENTER,onDragIn);
				
				hitBox.addEventListener(NativeDragEvent.NATIVE_DRAG_DROP,onDrop);
				hitBox.addEventListener(NativeDragEvent.NATIVE_DRAG_EXIT,onDragExit);
				hitBox.addEventListener(MouseEvent.MOUSE_DOWN, mouseDrag);
				//hitBox.addEventListener(MouseEvent.MOUSE_MOVE, onHitMove);
				hitBox.addEventListener(MouseEvent.CLICK, clickBox);
				hitBox.contextMenu = cm;
				alphaDiv.addChild(hitBox);
				
				timeDisplay.buttonMode = true;
				timeDisplay.visible = false;
				//timeDisplay.useHandCursor = true;//auto display fwd/bak hint text   << >>
				timeDisplay.x = int(stage.width/2 - timeDisplay.width/2);
				timeDisplay.y = int(stage.height - 15);
				timeDisplay.addEventListener(MouseEvent.CLICK, clickTime);
				timeDisplay.addEventListener(MouseEvent.RIGHT_CLICK, rightClickTime);
				alphaDiv.addChild(timeDisplay);
				
				//clean dead items out of the TrackList
				configureContextMenu();
				loadWindowPosition();
				
				//start timer for update song position
				var myTimer:Timer = new Timer(50);
				myTimer.addEventListener('timer', updateTime);
				myTimer.start();
            }

            private function configureContextMenu():void{
				stage.showDefaultContextMenu = true;
				cm.hideBuiltInItems();
				/*
				skip = new ContextMenuItem("Skip");
				skip.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, onSkipSelect);
				cm.customItems.push(skip);
				*/
				var star:ContextMenuItem = new ContextMenuItem("Star");
				star.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, onStarSelect);
				cm.customItems.push(star);
				
				var delTrack:ContextMenuItem = new ContextMenuItem("Delete");
				delTrack.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, onDeleteSelect);
				cm.customItems.push(delTrack);

				var pause:ContextMenuItem = new ContextMenuItem("Pause");
				pause.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, onPauseSelect);
				cm.customItems.push(pause);
				
				var clear:ContextMenuItem = new ContextMenuItem("Flush");
				clear.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, onClearSelect);
				cm.customItems.push(clear);

				var quit:ContextMenuItem = new ContextMenuItem("Quit");
				quit.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, onQuitSelect);
				cm.customItems.push(quit);

            }
                        
            private function loadWindowPosition():void{
				stage.nativeWindow.x = config.windowX;
				stage.nativeWindow.y = config.windowY;
				//stage.nativeWindow.width = prefsXML.windowState.@width;
				//stage.nativeWindow.height = prefsXML.windowState.@height;
            	stage.nativeWindow.visible = true;
            	erectPlayer();
            }

            private function erectPlayer():void{
				var storedRandomSong:String = config.currentRandomSong;
				
				//special case: first time app is run there's no song to play =(
				if(storedRandomSong == ''){return void;}
				
				//if the storedRandomSong is exists, play it
            	if(FileSystem.nativePathExists(storedRandomSong) == true){
            		holdingSavedSong = true;
            		randomPlay();
            	}else{//we randomly play a song like usual
            		randomPlay();
            	}
            }


//- UTIL -//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//
            private function removeDuplicates(tl:Array):Array{
			    var z:Array = tl.filter(function (a:*,b:int,c:Array):Boolean { return ((z ? z : z = new Array()).indexOf(a) >= 0 ? false : (z.push(a) >= 0)); }, this);
				return z;             		
            }
            
			/**
			 * This function takes an array of files and folders and retuns an array of just files
			 * This function also removes hidden files
			 * 
			 * @param files
			*/
			private function cleanFileArray(files:Array):Array{
				var arrayToReturn:Array = new Array();
				
				for each (var fileOrDir:File in files){
					if( fileOrDir.isDirectory ){
						// If we find a directory, call this method again to extract the files
						// This will recurse until there are only files in the returning array
						var subArray:Array = this.cleanFileArray( fileOrDir.getDirectoryListing() );
						arrayToReturn = arrayToReturn.concat( subArray ); // Combin the files with the arrayToReturn
					}else if( fileOrDir.isHidden ){
						// Skip hidden files
					}else{
						// If we have a file add it to the array of files to return
						arrayToReturn.push( fileOrDir );
					}
				}
				return arrayToReturn;
			}

			private function togglePlay():void{
				//modify sound?
				if(songPaused == true){ //is paused, must play
					_channel = _songCurrent.play(songPosition);
					_channel.addEventListener(Event.SOUND_COMPLETE, randomPlay);
				}
				else{ //is play, must pause
					songPosition = _channel.position;
					_channel.stop();
				}
				songPaused = !songPaused;
			}
			
			private function tagCurrentTrack():void{
				var currentRandomSong:String = config.currentRandomSong;
				if( currentRandomSong.indexOf(config.tagCopy) == -1 ){
					var renameName:String = currentRandomSong.replace('.mp3', config.tagCopy+'.mp3');
					//trace(renameName);
					
					FileSystem.rename(currentRandomSong, renameName);
					config.renameTrack(currentRandomSong, renameName);
					nfoDisplay.refreshWith(renameName);
				}
			}
			
			/**
			* randomPlay() the next track, delete the file from the filesystem.
			*/
			private function deleteCurrentTrack():void{
				var nativePathToNuke:String = config.currentRandomSong;
				randomPlay();			    
			    FileSystem.remove(nativePathToNuke);
				//config.removeTrack(currentRandomSong);//not 100% necessary since config.randomPlay() will delete any nulls it finds in the tracklist
			}

//- UI HANDLERS -//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//
			//drag & drop handlers
			public function onDragIn(event:NativeDragEvent):void{
			    NativeDragManager.dropAction = NativeDragActions.MOVE;
			    if(event.clipboard.hasFormat(ClipboardFormats.FILE_LIST_FORMAT)){
			    	NativeDragManager.acceptDragDrop(hitBox);
			        //NativeDragManager.acceptDragDrop(this); //'this' is the receiving component
			    }
			}
			public function onDrop(event:NativeDragEvent):void{
				var dropFiles:Array = event.clipboard.getData(ClipboardFormats.FILE_LIST_FORMAT) as Array;
				var addedCount:Number = 0;
				var skipCount:Number = 0;
				dropFiles = cleanFileArray(dropFiles);
				for each (var file:File in dropFiles){
					switch (file.extension){
						case "mp3":
							if(file.name.indexOf('.')==0){/*it's a hidden file with the . at the start*/
								break;
							}
							if(config.trackAlreadyAdded(file.nativePath)==false){
								//trace('onDrop() add file.nativePath: '+file.nativePath);
								config.addTrack(file.nativePath);
								addedCount++;
							}else{
								skipCount++;
							}
							break;
						default:
							//trace(file.name+" not a recognised file format"); 
					}
				}
				trace('onDrop() added '+addedCount+', skipped '+skipCount);
			}
			public function onDragExit(event:NativeDragEvent):void{
			    //trace("Drag exit event.");
			}
			
			private function windowClosingHandler(event:Event=null):void {
				config.saveWindowState(stage.nativeWindow.x, stage.nativeWindow.y);
				config.saveChannelState(_channel.position);
				config.writeXMLData();
			}

			//kb handlers			
			private function onKeyDown(e:KeyboardEvent):void{
				if(e.keyCode == Keyboard.SHIFT){
					shiftKeyDown = true;					
				}
			}
			private function onKeyUp(e:KeyboardEvent):void{
				if(e.keyCode == Keyboard.SHIFT){
					shiftKeyDown = false;
				}
			}

			public function mouseDrag(event:MouseEvent):void {
				stage.nativeWindow.startMove();
			}
			
			private function clickTime(event:MouseEvent):void{
				fastForward();
			}
			
			private function rightClickTime(event:MouseEvent):void{
				rewind();
			}
			
			//double click emulation
			private function clickBox(event:MouseEvent):void{
				clickNum++
				littleTimer.start()	
			}
			private function onBoxClick(event:TimerEvent):void{
				if(clickNum == 1){
					singleClickAction();
					littleTimer.stop();
					clickNum=0;
				}else{
					doubleClickAction();
					littleTimer.stop();
					clickNum=0;
				}
			}
			private function singleClickAction():void{
				if(shiftKeyDown == true){
				}
			}
			private function doubleClickAction():void{
				if(shiftKeyDown == true){
					previousPlay();
				}
				else{
					randomPlay();
				}
			}
			
            private function onAlphaDivOver(e:MouseEvent):void{
            	nfoDisplay.visible = true;
            	timeDisplay.visible = true;
            	alphaDiv.alpha = 1;
            }
            
            private function onAlphaDivOut(e:MouseEvent):void{
            	nfoDisplay.visible = false
            	timeDisplay.visible = false;//fade it out
            	alphaDiv.alpha = alphaValue;
            }
            
			
//- MENU ITEM HANDLERS -//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//
			private function onStarSelect(e:ContextMenuEvent=null):void{
				tagCurrentTrack();
			}
			private function onDeleteSelect(e:ContextMenuEvent=null):void{
				deleteCurrentTrack();
			}
			private function onPauseSelect(e:ContextMenuEvent=null):void{
				togglePlay();
			}
			private function onSkipSelect(e:ContextMenuEvent=null):void{
				randomPlay();
			}
			private function onQuitSelect(e:ContextMenuEvent=null):void{
				windowClosingHandler();
				//NativeApplication.nativeApplication.exit();
				nativeApplication.exit();
			}
			private function onClearSelect(e:ContextMenuEvent=null):void{
				config.removeAllTracks();
			}
			private function onConfigSelect(e:ContextMenuEvent=null):void{
				//todo =(
			}

//- SOUND ENGINE -//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//--//
			private function randomPlay(e:Event=null):void{
				var currentRandomSong:String;
				//chose a random song. if there are no songs, return
				if(holdingSavedSong == false){
					currentRandomSong = config.getRandomSong();
				}else{
					currentRandomSong = config.currentRandomSong;
					holdingSavedSong = false;
				}

				//special case: first time application is run, lets not freak out
				if(currentRandomSong == ''){
					return void;
				}
				
				//stop currentRandomSong
				if(_channel){
					_channel.stop();
					_channel = null;
				}
				//do a newie
				_channel = new SoundChannel();
				loadSound(currentRandomSong);
			}
			private function previousPlay():void{
				
			}
			private function loadSound(currentRandomSong:String):void{
				trace('shuffle: '+currentRandomSong);
				
				//var req:URLRequest = new URLRequest(currentRandomSong);
				//trying to get the app: off the URLRequest under OSX
				//maybe its possible to open the file and play the bytearray instead
				var file:File = new File();
				file = file.resolvePath(currentRandomSong);
				var req:URLRequest = new URLRequest(file.url);
				
				_songCurrent = new Sound();
				_songCurrent.addEventListener(IOErrorEvent.IO_ERROR, onSoundError);				
				_songCurrent.addEventListener(Event.COMPLETE, onSoundLoaded);
				_songCurrent.load(req);
			}
			private function onSoundLoaded(event:Event):void{
				_channel.stop();
				_channel = _songCurrent.play(config.savedPosition);
				_channel.addEventListener(Event.SOUND_COMPLETE, randomPlay);
				songPaused = false;
				config.savedPosition = 0;
				nfoDisplay.refreshWith(config.currentRandomSong);
				randomCover.refreshWith(config.currentRandomSong);
			}
			private function onSoundError(event:IOErrorEvent):void{
				trace("onSoundError(): " + event.text);
			}
			private function fastForward():void{
				songPosition = _channel.position + TRANSPORT_MS; //move ahead a magic number of milliseconds
				_channel.stop();
				if(_songCurrent.length > songPosition){
					_channel = _songCurrent.play(songPosition);
					_channel.addEventListener(Event.SOUND_COMPLETE, randomPlay);
					//todo: display currentSeconds/totalSeconds
					//todo: if currentSeconds > totalSeconds randomPlay()
					trace('songPosition: '+(int(songPosition/1000))+'/'+(int(_songCurrent.length/1000)));
				}else{
					randomPlay();
				}
				updateTime();
			}
			private function rewind():void{
				songPosition = _channel.position - TRANSPORT_MS; //move ahead a magic number of milliseconds
				_channel.stop();
				if(0 > songPosition){
					songPosition = 0;
				}
				_channel = _songCurrent.play(songPosition);
				_channel.addEventListener(Event.SOUND_COMPLETE, randomPlay);
				//todo: display currentSeconds/totalSeconds
				//todo: if currentSeconds > totalSeconds randomPlay()
				updateTime();
			}
			
			private function updateTime(e:TimerEvent=null):void{
				var elapsedSeconds:String = Utils.formatTime(_channel.position/1000);
				var totalSeconds:String = Utils.formatTime(_songCurrent.length/1000);
				timeDisplay.display(elapsedSeconds,totalSeconds);
				//display
			}			

        ]]>
    </mx:Script>
</mx:WindowedApplication>

